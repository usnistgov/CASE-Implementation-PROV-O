# This software was developed at the National Institute of Standards
# and Technology by employees of the Federal Government in the course
# of their official duties. Pursuant to title 17 Section 105 of the
# United States Code this software is not subject to copyright
# protection and is in the public domain. NIST assumes no
# responsibility whatsoever for its use by other parties, and makes
# no guarantees, expressed or implied, about its quality,
# reliability, or any other characteristic.
#
# We would appreciate acknowledgement if the software is used.

CONSTRUCT
{
  ?nAction prov:qualifiedUsage [
    a prov:Usage ;
    prov:entity ?nInput ;
    prov:atTime ?lTime ;
  ]
  .
}
WHERE {
  ?nAction
    a case-investigation:InvestigativeAction ;
    uco-core:hasFacet ?nActionReferences ;
    .

  OPTIONAL {
    ?nActionReferences
      uco-action:startTime ?lTime ;
      .
  }

  # When an object is recorded as an action input, use that object.
  # When no object is recorded as an action input, instead use
  # prov:EmptyCollection.
  {
    SELECT ?nActionReferences ?nInput
    WHERE {
      ?nActionReferences
        a uco-action:ActionReferences ;
        uco-action:object ?nInput ;
        .
    }
  }
  UNION
  {
    SELECT ?nActionReferences ?nInput
    WHERE {
      ?nActionReferences
        a uco-action:ActionReferences ;
        .
      BIND ( prov:EmptyCollection AS ?nInput )
      FILTER NOT EXISTS {
        ?nActionReferences
          uco-action:object ?nObject ;
          .
      }
    }
  }
}
